plugins {
    id 'fabric-loom' version '0.6-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
    accessWidener "src/main/resources/bewitchment.accesswidener"
}

repositories {
    maven {
        name "terraform wood api, trinkets"
        url "https://maven.terraformersmc.com"
    }
    maven {
        name "reach entity attributes"
        url "https://maven.jamieswhiteshirt.com/libs-release"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven {
        name "player ability lib, impersonate"
        url "https://dl.bintray.com/ladysnake/libs"
    }
    maven {
        name "cloth config, roughly enough items"
        url "https://maven.shedaniel.me"
    }
    maven {
        name "somnus"
        url "https://maven.theillusivec4.top"
    }
    maven {
        name "patchouli"
        url "https://maven.blamejared.com"
    }
    maven {
        name "trinkets"
        url "https://maven.abusedmaster.xyz"
    }
    maven {
        name "requiem"
        url "https://dl.bintray.com/ladysnake/mods"
    }
    maven {
        name "fall flying lib"
        url "https://dl.bintray.com/adriantodt/maven"
    }
    maven {
        name "jitpack"
        url "https://jitpack.io"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "com.terraformersmc.terraform-api:terraform-wood-api-v1:${terraform_wood_api_version}"
    include "com.terraformersmc.terraform-api:terraform-wood-api-v1:${terraform_wood_api_version}"
    modImplementation "com.jamieswhiteshirt:reach-entity-attributes:${reach_version}"
    include "com.jamieswhiteshirt:reach-entity-attributes:${reach_version}"
    modImplementation "com.github.emilyploszaj:step-height-entity-attribute:${step_height_version}"
    include "com.github.emilyploszaj:step-height-entity-attribute:${step_height_version}"
    modImplementation "io.github.ladysnake:PlayerAbilityLib:${pal_version}"
    include "io.github.ladysnake:PlayerAbilityLib:${pal_version}"

    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "top.theillusivec4.somnus:somnus-fabric:${somnus_version}"
    modImplementation("com.github.virtuoel:pehkui:${pehkui_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "io.github.ladysnake:Impersonate:${impersonate_version}"
    modImplementation "vazkii.patchouli:Patchouli:${patchouli_version}"
    modImplementation "dev.emi:trinkets:${trinkets_version}"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:${rei_version}"
    modRuntime "me.shedaniel:RoughlyEnoughItems:${rei_version}"
    modImplementation "io.github.emilyalexandra:nourish:${nourish_version}"
    modImplementation "io.github.ladysnake:requiem-api:${requiem_version}"
    modRuntime "io.github.ladysnake:requiem:${requiem_version}"
    modImplementation "net.adriantodt.fabricmc:fallflyinglib:${project.ffl_version}"
    modImplementation("com.github.apace100:origins-fabric:${project.origins_version}") {
        exclude(group: "*")
    }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        // select the repositories you want to publish to
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}